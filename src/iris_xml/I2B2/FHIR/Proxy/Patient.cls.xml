<?xml version="1.0" encoding="UTF-8"?>
<Export generator="IRIS" version="26">
<Class name="I2B2.FHIR.Proxy.Patient">
<Super>I2B2.FHIR.Proxy.Base</Super>
<TimeCreated>65801,40765.82218</TimeCreated>

<Property name="PatientId">
<Type>%String</Type>
<Parameter name="MAXLEN"/>
</Property>

<Property name="Patient">
<Type>public.patientdimension</Type>
</Property>

<Property name="MRN">
<Type>%String</Type>
<Parameter name="MAXLEN"/>
</Property>

<Property name="MRNDate">
<Type>%TimeStamp</Type>
</Property>

<Property name="LName">
<Type>%String</Type>
<Parameter name="MAXLEN"/>
</Property>

<Property name="FName">
<Type>%String</Type>
<Parameter name="MAXLEN"/>
</Property>

<Method name="KeyIdxExists">
<Description>
Check if resource exists
rsrcKey = $$$BuildRsrcKey(pResourceType, pResourceId), for example: "Patient/123"</Description>
<ClassMethod>1</ClassMethod>
<FormalSpec>rsrcKey:%String</FormalSpec>
<ReturnType>%Boolean</ReturnType>
<Implementation><![CDATA[
	s pResourceId = $p(rsrcKey, "/", 2)
	q ##class(public.patientdimension).patientdimensionpkExists(pResourceId)
]]></Implementation>
</Method>

<Method name="KeyIdxOpen">
<Description>
Get resource
rsrcKey = $$$BuildRsrcKey(pResourceType, pResourceId), for example: "Patient/123"</Description>
<ClassMethod>1</ClassMethod>
<FormalSpec>rsrcKey:%String,empty,tSC:%Status</FormalSpec>
<ReturnType>I2B2.FHIR.Proxy.Base</ReturnType>
<Implementation><![CDATA[
	s pResourceType = $p(rsrcKey, "/")
	s (patientId, pResourceId) = $p(rsrcKey, "/", 2)

	s patientDimension = ##class(public.patientdimension).patientdimensionpkOpen(pResourceId)
	i '$iso(patientDimension) {
		$$$ThrowFHIR($$$HSFHIRErrResourceNotFound, pResourceType, pResourceId,$$$OutcomeNotFound)
	}
	s proxy = ..%New()
	s proxy.PatientId = pResourceId
	s proxy.Patient = patientDimension

	// MRN
	s (SQLCODE,mrn,date) = ""
	&sql(select tval_char, import_date into :mrn, :date from "public".observation_fact where concept_cd = 'PHI:MRN' and patient_num = :patientId)
	i SQLCODE<0 {
		s errText="Ошибка исполнения SQL запроса ""select tval_char from ""public"".observation_fact where concept_cd = 'PHI:MRN' and patient_num = "_patientId_""":"_$SYSTEM.SQL.SQLCODE(SQLCODE)
		s tSC = $$$ERROR($$$GeneralError, errText) 
	} else {
		s proxy.MRN = mrn
		s proxy.MRNDate = date
	}

	// Patient.name
	s (lName,fName) = ""
	&sql(select tval_char into :lName from "public".observation_fact where concept_cd = 'PHI:LNAME' and patient_num = :patientId)
	i SQLCODE<0 {
		s errText="Ошибка исполнения SQL запроса ""select tval_char from ""public"".observation_fact where concept_cd = 'PHI:LNAME' and patient_num = "_patientId_""":"_$SYSTEM.SQL.SQLCODE(SQLCODE)
		s tSC = $$$ERROR($$$GeneralError, errText) 
	} else {
		s proxy.LName = lName
	}
	&sql(select tval_char into :fName from "public".observation_fact where concept_cd = 'PHI:FNAME' and patient_num = :patientId)
	i SQLCODE<0 {
		s errText="Ошибка исполнения SQL запроса ""select tval_char from ""public"".observation_fact where concept_cd = 'PHI:FNAME' and patient_num = "_patientId_""":"_$SYSTEM.SQL.SQLCODE(SQLCODE)
		s tSC = $$$ERROR($$$GeneralError, errText) 
	} else {
		s proxy.FName = fName
	}

	q proxy
]]></Implementation>
</Method>

<Method name="Transform">
<ReturnType>%Library.DynamicObject</ReturnType>
<Implementation><![CDATA[
	#dim patientResource As HS.FHIR.DTL.vR4.Model.Resource.Patient
	s sc = ##class(I2B2.FHIR.Transform.Patient).Transform(##this, .patientResource)
	i $$$ISERR(sc) {
		$$$ThrowFHIR($$$GeneralError, $system.Status.GetErrorText(sc))
	} else {
		q ##class(%DynamicObject).%FromJSON(patientResource.ToJSON())
	}
]]></Implementation>
</Method>
</Class>
</Export>
